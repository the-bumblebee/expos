// INTERRUPT 7 - WRITE SYSTEM CALL
// Since system call, registers are backed up in the user process

// PID obtained from [SYSTEM_STATUS_TABLE + 1]
// MODE FLAG in process table set to the system call number (5 for write)
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 5;

alias userSP R0;
userSP = SP;

// Save value of SP in user SP field in process table
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 13] = userSP;

// Setting SP to beginning of kernel stack
SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 11] * 512 - 1;

alias physicalPageNum R1;
alias offset R2;
alias fileDescPhysicalAddr R3;

// Computing the physical page no. of user stack, since in kernel mode
physicalPageNum = [PTBR + 2 * ((userSP - 4)/ 512)];
// Calculating the offset. (userP - 4) conatains the first argument.
// That is, the file descriptor is at (userSP - 4)
offset = (userSP - 4) % 512;
fileDescPhysicalAddr = (physicalPageNum * 512) + offset;
alias fileDescriptor R4;
fileDescriptor=[fileDescPhysicalAddr];

// Checking if file descriptor valid
if (fileDescriptor != -2) then
    // Returns -1 if file descriptor invalid
    alias physicalAddrRetVal R5;
    physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = -1;
else
    // Prints the second argument (userSP - 3) to terminal
    alias word R5;
    word = [[PTBR + 2 * ((userSP - 3) / 512)] * 512 + ((userSP - 3) % 512)];

    
    // Store all the registers in use to the stack.
    multipush(R0, R1, R2, R3, R4, R5);
    // Store function number of Terminal Write(3) in R1, PID to R2 and word in R3.
    R1 = TERMINAL_WRITE;
    R2 = [SYSTEM_STATUS_TABLE + 1];
    R3 = word;
    // Call Device Manager Module (Module 4)
    call DEVICE_MANAGER;
    // Terminal Write has no return value. So ignore R0 and pop back registers.
    multipop(R0, R1, R2, R3, R4, R5);

    // Return value indicating success
    alias physicalAddrRetVal R6;
    physicalAddrRetVal = ([PTBR + 2 * (userSP - 1)/ 512] * 512) + ((userSP - 1) % 512);
    [physicalAddrRetVal] = 0;

endif;

// Setting SP back to user SP
SP = userSP;

// Resetting MODE FLAG to 0 which indicates process running in user mode
[PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1] * 16 + 9] = 0;

// Passing control back to the user program
ireturn;